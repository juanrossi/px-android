apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

Properties properties = new Properties()

if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

ext {
    versionToDeploy = version_to_deploy as String
    artifactLib = 'services'
    libraryName = 'mercadopago-sdk_android_services'
    libraryDescription = 'MercadoPago Services Android SDK'
    siteUrl = 'https://github.com/mercadopago/px-android_services'
    gitUrl = 'https://github.com/mercadopago/px-android_services.git'
    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    groupName = "com.mercadopago"
}

version = versionToDeploy
group = groupName
archivesBaseName = artifactLib

task androidJavadocs(type: Javadoc) {
    failOnError = false
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // JDK 1.8 is more strict then 1.7. Have JDK 1.8 behave like 1.7 for javadoc generation
    if (org.gradle.internal.jvm.Jvm.current().getJavaVersion() == JavaVersion.VERSION_1_8) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

afterEvaluate {
    androidJavadocs.classpath += project.android.libraryVariants.toList().first().javaCompile.classpath
}

install {
    group = groupName
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId group
                artifactId artifactLib

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id 'valeria.serber'
                        name 'Valeria Serber'
                        email 'valeria.serber@mercadolibre.com'
                    }
                    developer {
                        id 'angeles.arlanti'
                        name 'Maria De Los Angeles Arlanti'
                        email 'angeles.arlanti@mercadolibre.com'
                    }
                    developer {
                        id 'nicolas.fortunato'
                        name 'Nicolas Fortunato'
                        email 'nicolas.fortunato@mercadolibre.com'
                    }
                    developer {
                        id 'matias.romar'
                        name 'Matias Romar'
                        email 'matias.romar@mercadolibre.com'
                    }
                    developer {
                        id 'lucas.bais'
                        name 'Lucas Bais'
                        email 'lucas.bais@mercadolibre.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

bintray {
    version = versionToDeploy
    user = properties.getProperty("bintray.user", "")
    key = properties.getProperty("bintray.apikey", "")

    override = true

    configurations = ['archives']
    pkg {
        repo = 'PX'
        name = libraryName
        desc = libraryDescription
        userOrg = 'mercadopago'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password", "")
            }
        }
    }
}